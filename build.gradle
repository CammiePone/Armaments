plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id 'idea'
	id 'maven-publish'
}

def ENV = System.getenv()
def buildTime = ENV.BUILD_TIME ?: new Date().format('yyyyMMddHHmmss')
boolean isPreviewBuild = !ENV.TAG || ENV.TAG.matches(".+-.+")
def buildNumber = !ENV.TAG ? ("${ENV.BUILD_NUMBER ? "build.${ENV.BUILD_NUMBER}" : buildTime}") : ""

version = (ENV.TAG ?: "${libs.versions.minecraft.get()}-development") + ((isPreviewBuild && !ENV.TAG) ? "+${buildNumber}" : "")
group = project.maven_group
archivesBaseName = project.archives_base_name

loom {
	mods {
		"${mod_id}" {
			sourceSet("main")
		}
	}
}

repositories {
	maven {
		name = "Team Resourceful"
		url = "https://nexus.resourcefulbees.com/repository/maven-public/"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com"
	}
	maven {
		name = "Ladysnake"
		url = "https://maven.ladysnake.org/releases"
	}
	maven {
		name = "Upcraft"
		url = "https://maven.uuid.gg/releases"
	}
}

dependencies {
	minecraft libs.minecraft
	mappings loom.officialMojangMappings()
	modImplementation libs.fabric.loader

	modImplementation libs.fabric.api
	modImplementation libs.resourceful.config
	modImplementation libs.bundles.cca

	modImplementation (libs.sparkweave) {
		exclude(group: "io.github.llamalad7")
	}

	modImplementation libs.modmenu
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE.md") {
		rename { "LICENSE_${archivesBaseName}.md" }
	}

	manifest.mainAttributes(
		"Implementation-Title": project.archivesBaseName,
		"Implementation-Version": project.version,
		"Maven-Artifact": "${project.group}:${project.archivesBaseName}:${project.version}".toLowerCase(Locale.ROOT),
		"Built-On-Minecraft": libs.versions.minecraft.get(),
		"Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
	)
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId "Enhanced-Doors"
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_UPLOAD_URL) {
			maven {
				url = ENV.MAVEN_UPLOAD_URL
				credentials {
					username = ENV.MAVEN_UPLOAD_USERNAME
					password = ENV.MAVEN_UPLOAD_PASSWORD
				}
			}
		}
	}
}

idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}
